;+
; NAME:
;    nufab_screen_event
;
; PURPOSE:
;    Dispatch events generated by the nuFAB screen to the
;    appropriate event handlers.
;
; MODIFICATION HISTORY:
; 01/20/2014 Written by David G. Grier, New York University
;
; Copyright (c) 2014 David G. Grier
;-
function nufab_screen_event, event

COMPILE_OPT IDL2, HIDDEN

case event.type of
   0: begin                           ; mouse down event
      case event.modifiers of
         0: nuevent_trapselect, event ; no modifier
         1: nuevent_trapadd, event    ; shift-click
         2: nuevent_trapremove, event ; control-click
      endcase
   end
   1: nuevent_mouseup, event          ; mouse up event
   2: nuevent_motion, event           ; mouse drag event
   6: return, nuevent_keyboard(event) ; keyboard event
   7: nuevent_wheel, event            ; mouse wheel
   else: return, event                ; pass unrecognized events back
endcase

return, 0

end
